#version 460
#extension GL_GOOGLE_include_directive : enable // Include files
#extension GL_EXT_ray_tracing : enable // Raytracing

#include "bindpoints.glsl"
#include "common/camera.glsl"
#include "common/xteanoise.glsl"
#include "shading/constants.glsl"
#include "directlight.glsl"
#include "irradiancecache/bindin.glsl"
#include "irradiancecache/bindout.glsl"
#include "visitest/visitest.glsl"

void main()
{
	const uvec3 irrTexel = gl_LaunchIDEXT.xyz;
	const vec3 origin = transformIrradiancePixelToWorld(irrTexel);
	uint seed = CalculateSeedXTEA(irrTexel, TracerConfig.RngSeed);

	vec3 irradiance;
	uint mode = irradianceCacheMode();
	if (mode == MODE_INDIRECT_TRACE_DIRECT_TRACE || mode == MODE_DIRECT_TRACE || mode == MODE_DEBUG_PATTERN) {
		// ic unused
		irradiance = vec3(0);
	} else if (mode == MODE_INDIRECT_IC_DIRECT_TRACE || mode == MODE_INDIRECT_IC) {
		irradiance = vec3(0);
		if (!irradianceCacheClearCache()) {
			irradiance += readIrradianceCache(irrTexel).xyz;
		}
		irradiance += collectDirectLight(origin, seed);
	} else if (mode == MODE_DIRECT_IC) {
		irradiance = collectDirectLight(origin, seed);
	} else {
		// not implemented! -> specific recognizable color with NaNs
		irradiance = vec3(1, 0.5, 0/0);
	}
	writeIrradianceCache(irrTexel, vec4(irradiance, 0.0));
}
